buildscript {
    ext.dokka_version = '0.9.15'
    ext.kotlin_version = '1.1.51'

    repositories {
        jcenter()
    }

    dependencies {
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:$dokka_version"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'org.junit.platform:junit-platform-gradle-plugin:1.0.0'
    }
}

plugins {
    id "com.jfrog.bintray" version "1.7.3"
}

apply plugin: 'jacoco'
apply plugin: 'java-library'
apply plugin: 'kotlin'
apply plugin: 'maven-publish'
apply plugin: 'org.jetbrains.dokka'
apply plugin: 'org.junit.platform.gradle.plugin'

def dokkaOutputDir = "$buildDir/javadoc"

group 'com.exsilicium.scripture'
version '0.1.1'

repositories {
    jcenter()
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jre8:$kotlin_version"

    testImplementation("org.junit.jupiter:junit-jupiter-api:5.0.1")

    testRuntime("org.junit.platform:junit-platform-launcher:1.0.0")
    testRuntime("org.junit.jupiter:junit-jupiter-engine:5.0.1")
    testRuntime("org.junit.vintage:junit-vintage-engine:4.12.0")
}

dokka {
    outputFormat = 'html'
    outputDirectory = dokkaOutputDir
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    publications = ['MyPublication']
    pkg {
        repo = 'scripture'
        name = project.name
        userOrg = 'ex-silicium'
        version {
            name = project.version.toString()
            desc = 'Minor patch with better ScriptureReference#toString'
            vcsTag = 'v' + project.version
        }
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: dokka) {
    classifier = 'javadoc'
    from dokkaOutputDir
}

def pomConfig = {
    scm {
        url "https://github.com/Ex-Silicium/scripture-core.git"
    }
}

publishing {
    publications {
        MyPublication(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            groupId group
            artifactId project.name
            version version
            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'Tools for working with scripture references')
                root.appendNode('name', 'Scripture Core')
                root.appendNode('url', 'https://github.com/Ex-Silicium/scripture-core')
                root.children().last() + pomConfig
            }
        }
    }
}