buildscript {
    ext.dokka_version = '0.9.18'
    ext.kotlin_version = '1.3.41'

    repositories {
        jcenter()
    }

    dependencies {
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:$dokka_version"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins {
    id "com.jfrog.bintray" version "1.8.0"
}

apply plugin: 'jacoco'
apply plugin: 'java-library'
apply plugin: 'kotlin'
apply plugin: 'maven-publish'
apply plugin: 'org.jetbrains.dokka'

def dokkaOutputDir = "$buildDir/javadoc"

group = 'com.exsilicium.scripture'
version = '0.1.3-SNAPSHOT'

repositories {
    jcenter()
}

dependencies {
    // https://mvnrepository.com/artifact/org.jetbrains.kotlin/kotlin-stdlib-jdk8
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

    // https://mvnrepository.com/artifact/org.junit.jupiter/junit-jupiter-api
    testCompile("org.junit.jupiter:junit-jupiter-api:5.5.0")
    testRuntime("org.junit.jupiter:junit-jupiter-engine:5.5.0")
}

dokka {
    outputFormat = 'html'
    outputDirectory = dokkaOutputDir
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    publications = ['ScriptureCoreJar']
    pkg {
        repo = 'scripture'
        name = project.name
        userOrg = 'ex-silicium'
        version {
            name = project.version.toString()
            desc = 'Minor patch with better VerseRanges#toString'
            vcsTag = 'v' + project.version
        }
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: dokka) {
    classifier = 'javadoc'
    from dokkaOutputDir
}

def pomConfig = {
    scm {
        url "https://github.com/Ex-Silicium/scripture-core.git"
    }
}

publishing {
    publications {
        ScriptureCoreJar(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            groupId group
            artifactId project.name
            version = version
            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'Tools for working with scripture references')
                root.appendNode('name', 'Scripture Core')
                root.appendNode('url', 'https://github.com/Ex-Silicium/scripture-core')
                root.children().last() + pomConfig
            }
        }
    }
}

test {
    useJUnitPlatform()
}
